/*
 * Copyright (C) 2018. OpenLattice, Inc.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 *
 * You can contact the owner of the copyright at support@openlattice.com
 *
 */

plugins {
    id "java"
    id "eclipse"
    id "idea"
    id "jacoco"
    id 'maven-publish'
    id 'checkstyle'
    id 'signing'
    id 'com.github.spotbugs'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.spring'
}

description = "Scribe renders is templated render system for e-mails"
group = "com.openlattice"

ext.projectName = 'scribe'
ext.scmUrl = 'scm:git:https://github.com/openlattice/scribe.git'
ext.connectionUrl = 'scm:git:https://github.com/openlattice/scribe.git'
ext.developerConnectionUrl = 'scm:git:https://github.com/openlattice/scribe.git'

apply from: "../gradles/openlattice.gradle"
apply plugin: 'kotlin'
sourceCompatibility=JavaVersion.VERSION_11
ext.targetCompatibility = JavaVersion.VERSION_11

checkstyle {
    toolVersion = '7.4'
    showViolations = false
    config project.resources.text.fromUri('https://raw.githubusercontent.com/openlattice/openlattice/develop/gradles/checkstyle.xml')
}

tasks.withType(Checkstyle) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
spotbugs {
    reportLevel = 'high'
    toolVersion = "${spotbugs_annotations_version}"
}

spotbugsMain {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jacoco {
    toolVersion = "${project.jacoco_tool_version}"
}

test {
    ignoreFailures = true
}

dependencies {
    spotbugsPlugins "com.h3xstream.findsecbugs:findsecbugs-plugin:${findsecbugs_version}"

    compileOnly "net.jcip:jcip-annotations:${jcip_version}"
    testCompileOnly "net.jcip:jcip-annotations:${jcip_version}"

    compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs_annotations_version}"
    testCompileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs_annotations_version}"

    /*
     * JAVAMAIL
     * Java email client.
     */
    implementation "org.jodd:jodd-mail:${jodd_mail_version}"

    /*
     * @Inject and @Nullable support
     */
    implementation "javax.inject:javax.inject:${javax_inject_version}"

    implementation "com.google.guava:guava:${guava_version}"
    implementation "commons-codec:commons-codec:${commons_codec_version}"
    implementation "org.apache.commons:commons-lang3:${commonsLang_version}"
    implementation "com.fasterxml.jackson.module:jackson-module-jaxb-annotations:${jackson_version}"

    implementation "com.hazelcast:hazelcast:${hazelcast_version}"

    compile "com.esotericsoftware:kryo-shaded:${kryo_shaded_version}"
    compile "com.esotericsoftware:minlog:${minlog_version}"
    compile "com.esotericsoftware:reflectasm:${reflectasm_version}"
    compile "org.objenesis:objenesis:${objenesis_version}"

    implementation "com.openlattice:rhizome:${rhizome_client_version}"

    if( project.hasProperty('developmentMode') && project.developmentMode ) {
        testImplementation(project(path: ":rhizome-client", configuration: "tests"))
    } else {
        testImplementation "com.openlattice:rhizome-client:${rhizome_client_version}:tests"
    }

    /*
     * TESTING
     */
    testImplementation "junit:junit:${junit_version}"
    testImplementation( "org.mockito:mockito-all:${mockito_version}" ) {
        exclude module : 'hamcrest'
        exclude module : 'hamcrest-core'
    }
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

install {
	configuration = configurations.published
}

eclipse {
    ext.downloadSources = true
    ext.downloadJavadoc = true
    ext.sourceCompatibility=JavaVersion.VERSION_11
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
